#!/usr/bin/env perl

use strict;
use warnings;
use Archive::Tar;
use Config;
use Digest::MD5 qw(md5 md5_hex md5_base64);
use File::Basename;
use File::Copy;
use File::Spec;
use File::Fetch;
use File::Path qw(make_path remove_tree);
use Fcntl qw(:DEFAULT :flock);
use Getopt::Long;
use IPC::Cmd qw(can_run run run_forked);
use IO::Compress::Gzip qw(gzip $GzipError) ;
use IO::Uncompress::Gunzip qw(gunzip $GunzipError);
use Time::HiRes qw(gettimeofday tv_interval);

#version number
my $tamock_version = "1.3.0";

#absolute path of install dir
my $wd;
if (-l $0 ) {
	$wd = File::Spec->rel2abs(readlink($0));
} else {
	$wd = File::Spec->rel2abs($0);
}

if (! -d $wd) {
	$wd = dirname($wd);
}

my $t0 = [gettimeofday];

#default NCBI assembly summary table
my $ncbi_as_uri = "ftp://ftp.ncbi.nlm.nih.gov/genomes/refseq/assembly_summary_refseq.txt";

#config		refseq dir
my ($readsf,$readsr,$readsu,$cent_krep,$cent_tab) = (undef,undef,undef,undef,undef);
my ($index,$assembly_summary,$update_as,$refseq_folder,$no_strain_ra,$modegz,$qprof1,$qprof2,$rnsim,$rntot,
	$length,$verbose,$outdir,$install_deps,$install_test,$install_test_debug,$domains,$keep);

#defaults
my $threads = 1;
my $mfl = 200;
my $sfl = 10;
my $illumina_model = "custom";

GetOptions(	"outdir=s" => \$outdir,
			
			"forward|1=s" => \$readsf,
			"reverse|2=s" => \$readsr,
			"unpaired|U=s" => \$readsu,
			"index|x=s" => \$index,
			"threads:i" => \$threads,
			
			"domains=s" => \$domains,
			"refgenomes|R=s" => \$refseq_folder,
			"assembly-summary=s" => \$assembly_summary,
			"no-reassign" => \$no_strain_ra,
			"keep" => \$keep,
			"centrifuge-kreport=s" => \$cent_krep,
			"centrifuge-out=s" => \$cent_tab,
			
			"illumina-model|M=s" => \$illumina_model,
			"mean-fragment-length=i" => \$mfl,
			"sd-fragment-length=i" => \$sfl,
			"length:i" => \$length,
			"gzip" => \$modegz,
			"qprof1=s" => \$qprof1,
			"qprof2=s" => \$qprof2,
			"rn-sim:i" => \$rnsim,
			"rn-total=i" => \$rntot,
			
			"install-deps" => \$install_deps,
			"install-test" => \$install_test,
			"install-test-debug" => \$install_test_debug,
			"ncbi-sum-update" => \$update_as,
			"verbose" => \$verbose,
			
			"help" => \&print_help);

if ($install_deps) {
	install_dependencies();
}

if ($install_test) {
	test_installation();
}

if ($update_as) {
	$outdir = $wd;
}

##########################################################################
#sanity checks

########################
### outdir ###

if (defined $outdir && -d $outdir) {
	print "WARNING: Directory already exists, results might be overwritten\nContinuing in 10 sec...\n";
	#give 10 secs to kill command if wrong dir is provided
	sleep 10;
} else {
	die "No output directory given\n" unless $outdir;
	mkd("$outdir");
}

########################
### assembly summary table ###

if (! defined $assembly_summary) {
	if (-r "${wd}/assembly_summary.txt" && ! $update_as) {
		$assembly_summary = "${wd}/assembly_summary_refseq.txt";
	} else {
		if ($update_as) {
			#update assembly file in install dir
			$assembly_summary = fetchfile("$ncbi_as_uri","$wd");
			exit;
		} else {
			p_lock(0,1,"$outdir/tamock.log",
				"Downloading '$ncbi_as_uri' to current directory ...\n");
			$assembly_summary = fetchfile("$ncbi_as_uri");
		}
	}
} else {
	die "Can't read provided file '$assembly_summary'\n" unless (-r $assembly_summary);
}

########################
### selected domains ###
print "The following domains are selected for simulation:\n";
if (! $domains) {
	$domains = "B";
	print "-Bacteria\n";
} else {
	foreach my $domain (split(",",$domains)) {
		if ($domain !~ /[EAVB]/) {
			die "Unknown value '$domain' for option -d, valid domains are [EAVB], separated by comma for multiple selections\n";
		} elsif ($domain eq "E") {
			print "-Eukaryota\n";
		} elsif ($domain eq "A") {
			print "-Archaea\n";
		} elsif ($domain eq "B") {
			print "-Bacteria\n";
		} elsif ($domain eq "V") {
			print "-Viruses\n";
		} else {
			die "BUG: errorneous domain option selection for option $domain\n";
		}
	}
}
print "\n";

########################
### dependencies ###
my ($centrifuge_path,$kreport_path,$art_path,$artprofiler_path);

#centrifuge
if ( -x "${wd}/centrifuge/bin/centrifuge" && -x "${wd}/centrifuge/bin/centrifuge-kreport" ) {
	$centrifuge_path = "${wd}/centrifuge/bin/centrifuge";
	$kreport_path = "${wd}/centrifuge/bin/centrifuge-kreport";
} else {
	$centrifuge_path = can_run('centrifuge') 
		or die "'centrifuge' is not installed, please run $0 --install-deps\n";
	$kreport_path = can_run('centrifuge-kreport') 
		or die "'centrifuge-kreport' is not installed, please run $0 --install-deps\n";
		p_lock(1,$verbose,"$outdir/tamock.log",
		"Using system-wide installation of centrifuge '$centrifuge_path'");
}



#art
if ( -x "${wd}/art/bin/art_illumina" && -x "${wd}/art/bin/art_profiler_illumina") {
	$art_path = "${wd}/art/bin/art_illumina";
	$artprofiler_path = "${wd}/art/bin/art_profiler_illumina"; 
} else {
	$art_path = can_run('art_illumina') 
		or die "'art_illumina' is not installed, please run $0 --install-deps\n";
		p_lock(1,$verbose,"$outdir/tamock.log",
		"Using system-wide installation of art_illumina '$art_path'");	
	$artprofiler_path = can_run('art_profiler_illumina') 
		or die "'art_profiler_illumina' is not installed, please run $0 --install-deps\n";
		p_lock(1,$verbose,"$outdir/tamock.log",
		"Using system-wide installation of art_profiler_illumina '$artprofiler_path'");	
}

########################
### input sequence files ###
my $semode;
{
	#either paired or unpaired reads can be unitialized
	no warnings 'uninitialized';
	if (! (( -r $readsf && -r $readsr) || -r $readsu) ) {
		die "ERROR: No sequence files provided\n";
	}
	#flag SE mode, default expecting PE mode
	$semode++ unless (-r $readsf && -r $readsr);
}
#change all reads to absolute paths to make ART custom profile work
$readsf = File::Spec->rel2abs($readsf);
$readsr = File::Spec->rel2abs($readsr) if $readsr;
$readsu = File::Spec->rel2abs($readsu) if $readsu;

########################
### centrifuge ###

#index
if (! $index) {
	if (-r "${wd}/centrifuge-index/p+h+v.1.cf") {
		$index = "${wd}/centrifuge-index/p+h+v";
		p_lock(0,$verbose,"$outdir/tamock.log",
				"Using default index '${wd}/centrifuge-index/p+h+v' for classification\n");
	} else {
		die "No index provided or found, please provide centrifuge-index\n";
	}
} else {
	if (! -r "${index}.1.cf" ) {
		die "ERROR: Provided centrifuge-index '$index' not readable\n";
	} else {
		p_lock(0,$verbose,"$outdir/tamock.log",
				"Using index '${index}' for classification\n");
	}
}

#centrifuge-files
if (defined $cent_krep) {
	die "ERROR: '$cent_krep' not readable\n" unless (-r $cent_krep);
}
if (defined $cent_tab) {
	die "ERROR: '$cent_tab' not readable\n" unless (-r $cent_tab);
}

########################
### refseq genome folder ###

if (! $refseq_folder) {
	if (-d "${wd}/refseq-genomes") {
		$refseq_folder = "${wd}/refseq-genomes";
	} else {
		die "ERROR: No directory for reference genomes given, please specify directory with -R\n";
	}
} else {
	#test if writeable
	if (! -d $refseq_folder) {
		mkd("$refseq_folder");
	}
	my $touch = w_file("$refseq_folder/write_test_refseq");
	close $touch;
	rmrf("$refseq_folder/write_test_refseq");
}

########################
### ART parameters ###

#determine readlength of inputfile if not provided
if (! defined $length) {
	my $IN;
	if ($semode) {
		$IN = r_file("$readsu");
	} else {
		$IN = r_file("$readsf");
	}
	$length = 0;
	while (my $line = <$IN>) {
		next unless (($.-2)%4 == 0);
		last if $. > 1000;
		chomp $line;
		my $readlength = length($line);
		$length = $readlength if ($readlength > $length) 
	}
	close $IN;
}
#if a custom error profile is not provided, mode is custom
if (defined $qprof1) {
	$illumina_model = "custom";
}

#positive values for min species abundance, read length, mean fragment lenght, sd fragment length, threads
my %inttest = (
	'length' => $length,
	'mean-fragment-length' => $mfl,
	'sd-fragment-length' => $sfl,
	'threads' => $threads
	);
for my $para (keys %inttest) {
	if (! $inttest{$para} =~ /^[1-9]+[0-9]*$/) {
		die "ERROR: Value '$inttest{$para}' for parameter '$para' is not a positive integer greater than 0\n";
	}
}

#ART models
my @art_models = ("GA1", "GA2", "HS10", "HS20", "HS25", "HSXn", "HSXt", "MinS", "MSv1", "MSv3", "NS50", "custom");
my $flag;
foreach my $artmodel (@art_models) {
	$flag++ if( $artmodel eq $illumina_model)
}
die "ERROR: '$illumina_model' is not a valid model for ART. ",
"Please check documentation at https://github.com/gerners/tamock or '$art_path -h' for valid options\n" unless $flag;

#custom models
if (defined $qprof1) {
	die "ERROR: Can't read ART quality profile '$qprof1'\n" unless (-r $qprof1);
}
if (defined $qprof2) {
	die "ERROR: Can't read ART quality profile '$qprof2'\n" unless (-r $qprof2);
}

##########################################################################
#run centrifuge

my $t1 = [gettimeofday];

if (! (defined $cent_krep && defined $cent_tab)) {
	my $cent_cmd = "$centrifuge_path --out-fmt tab -x $index -p $threads";
	if ($readsf && $readsr) {
		$cent_cmd = "$cent_cmd -1 $readsf -2 $readsr";
	}
	if ($readsu) {
		$cent_cmd = "$cent_cmd -U $readsu";
	}
	$cent_cmd = "$cent_cmd -S $outdir/classification_out/centrifuge.out --report-file $outdir/classification_out/centrifuge.report";
	
	p_lock(0,1,"$outdir/tamock.log",
		"#################################################\nClassifying sequences...\n");
		
	mkd("$outdir/classification_out");
	runcmd("$cent_cmd",$verbose);
	runcmd("$kreport_path -x $index $outdir/classification_out/centrifuge.out > $outdir/classification_out/centrifuge.kreport",$verbose);
}

my $msg = "Finished classification with runtime " . runtime(tv_interval($t1)) ;
p_lock(0,1,"$outdir/tamock.log", "$msg\n");

##########################################################################
#create benchmarks

########################
#custom quality profiles

my $t2 = [gettimeofday];

if ("$illumina_model" eq "custom") {
	
	p_lock(0,1,"$outdir/tamock.log",
	"#################################################\nDetermining sequence quality profile(s)...\n");
	
	#link input filenames with specific names to ensure proper filenames for art_profiler
	mkd("$outdir/tmpreads/art_profile");
	
	#single-end
	my $read_suffix;
	my $seqprof_cmd = "$artprofiler_path ${outdir}/tmpreads/art_profile/customProfile";
	if ($semode) {
		if ($readsu =~ /\.?(\w*(\.gz)?)$/) {
			$read_suffix = $1;
		} else {
			die "Unexpected filename of '$readsu', probably contains non-alphanumeric symbols...\n";
		}
		my $linkpath = "${outdir}/tmpreads/art_profile/reads.${read_suffix}";
		symlink("$readsu","$linkpath") or die "Creating symlink '$linkpath' failed: $!";
		
		$seqprof_cmd .= " ${outdir}/tmpreads/art_profile/ ${read_suffix} 2";
		runcmd("$seqprof_cmd",$verbose);
		
	#paired
	} else {
		my $i = 1;
		for my $readpath ($readsf,$readsr) {
			if ($readpath =~ /\.?(\w*(\.gz)?)$/) {
				$read_suffix = $1;
			} else {
				die "Unexpected filename of '$readpath', probably contains non-alphanumeric symbols...\n";
			}
			my $linkpath = "$outdir/tmpreads/art_profile/reads_${i}.${read_suffix}";
			symlink("$readpath","$linkpath") or die "Creating symlink '$linkpath' for '$readpath' failed: $!";
			$i++;
		}
		$seqprof_cmd .= " ${outdir}/tmpreads/art_profile/ ${read_suffix} 2";
		runcmd("$seqprof_cmd",$verbose);
	}
}
########################
#create benchmark profile

p_lock(0,1,"$outdir/tamock.log",
	"########################\nCreating benchmark sequences...\n");
my $profile_cmd = "${wd}/scripts/benchmark_profile_kreport.pl -a $assembly_summary";
if ($cent_krep) {
	$profile_cmd .= " -k $cent_krep";
} else {
	$profile_cmd .= " -k $outdir/classification_out/centrifuge.kreport";
}

$profile_cmd = "$profile_cmd -r $refseq_folder -o $outdir/classification_out/ -v";
$profile_cmd .= " --no-reassign" if $no_strain_ra;
if ($domains) {
	$profile_cmd .= " -d $domains";
}
$profile_cmd .= " --rn-sim $rnsim" if $rnsim;

print("INFO: Downloading required genomes from RefSeq might take multiple hours...\n");
runcmd("$profile_cmd",$verbose);

########################
#create benchmarks

my $benchmark_cmd= "${wd}/scripts/create_benchmarks_art.pl -p $outdir/classification_out/fullprofile.tsv -l $length";
$benchmark_cmd.= " --mean-fragment-length $mfl --sd-fragment-length $sfl -o $outdir/tmpreads -R $refseq_folder ";

if ("$illumina_model" eq "custom") {
	#external precalculated error profile
	if (defined $qprof1) {
		$benchmark_cmd.= " --qprof1 $qprof1" ;
		$benchmark_cmd.= " --qprof2 $qprof2" if (defined $qprof2);
	#calculated error profiles
	} else {
		if ($semode) {
			$benchmark_cmd.= " --qprof1 ${outdir}/tmpreads/art_profile/customProfile.txt" ;
		} else {
			$benchmark_cmd.= " --qprof1 ${outdir}/tmpreads/art_profile/customProfileR1.txt";
			$benchmark_cmd.= " --qprof2 ${outdir}/tmpreads/art_profile/customProfileR2.txt";
		}
	}
#prebuild ART error profiles	
} else {
	$benchmark_cmd.= " -M $illumina_model";
}

$benchmark_cmd .= " --gzip" if $modegz;
$benchmark_cmd .= " --single-end" if $semode;
$benchmark_cmd .= " --debug" if $install_test_debug;
runcmd("$benchmark_cmd",$verbose);

#remove ART input files which are not needed anymore

########################
#separate reads with reference genome assigned from reads without 

my $extractbac_cmd = "${wd}/scripts/extract_refreads_kreport.pl";
if ($cent_tab) {
	$extractbac_cmd .= " -c $cent_tab";
} else {
	$extractbac_cmd .= " -c $outdir/classification_out/centrifuge.out";
}
$extractbac_cmd .= " --gzip" if $modegz;
$extractbac_cmd .= " -x $outdir/classification_out/taxa_w_refgenome.tsv -o $outdir/tmpreads";
$extractbac_cmd .= " $readsf $readsr" if ($readsf && $readsr);
$extractbac_cmd .= " $readsu" if ($readsu);

runcmd("$extractbac_cmd",$verbose);

#remove temporary folder with all taxa with reference genome for read separation
rmrf("$outdir/classification_out/taxa_w_refgenome.tsv");
########################
#cat result files

#single end mode, only se file expected
if ($semode) {
	my $read_b = basename($readsu);
	my ($read_nref,$read_fn,$read_sf);
	if ($read_b =~ /(\w+\.)(.+)/) {
		$read_nref = "$outdir/tmpreads/${1}not_repl.${2}";
		$read_fn = $1;
		$read_sf = $2;
	} else {
		die "Unexpected format of inputfile '$read_b', expected <name.suffix(es)>\n";
	}
	#FNR = no reference genome reads, FB = benchmark reads, FWGZ = merged outputfile
	
	my $bmfull = gz_path_check("$outdir/benchmark_${read_fn}${read_sf}",$modegz);
	my $FWGZ = w_file("$bmfull");
	
	$read_nref = gz_path_check("$read_nref",$modegz);
	my $FNR = r_file("$read_nref");
	
	while (my $line = <$FNR>) {
		print $FWGZ $line;
	}
	close $FNR;
	
	my $bmonly = gz_path_check("$outdir/tmpreads/simulated.fq",$modegz);
	my $FBO = r_file("$bmonly");
	
	while (my $line = <$FBO>) {
		print $FWGZ $line;
	}
	close $FBO;
	close $FWGZ;
	 
} else {
	for my $file ($readsf,$readsr) {
		my $read_b = basename($file);
		my ($read_nref,$read_fn,$read_sf);
		if ($read_b =~ /(\w+\.)(.+)/) {
			$read_nref = "$outdir/tmpreads/${1}not_repl.${2}";
			$read_fn = $1;
			$read_sf = $2;
		} else {
			die "Unexpected format of inputfile '$read_b', expected <name.suffix(es)>\n";
		}
		
		my $bmfull = gz_path_check("$outdir/benchmark_${read_fn}${read_sf}",$modegz);
		my $FWGZ = w_file("$bmfull");
		
		$read_nref = gz_path_check("$read_nref",$modegz);
		my $FNR = r_file("$read_nref");
		
		while (my $line = <$FNR>) {
			print $FWGZ $line;
		}
		close $FNR;
				
		my $FBO;
		if ($file eq $readsf) {
			my $bmonly1 = gz_path_check("$outdir/tmpreads/simulated_1.fq",$modegz);
			$FBO = r_file("$bmonly1");
		} else {
			my $bmonly2 = gz_path_check("$outdir/tmpreads/simulated_2.fq",$modegz);
			$FBO = r_file("$bmonly2");
		}
		while (my $line = <$FBO>) {
			print $FWGZ $line;
		}		
		close $FBO;
		close $FWGZ;
		
	}
	#in paired end mode, only paired reads are simulated, copy non-reference single end file if present
	if (defined $readsu) {
		my $read_b = basename($readsu);
		my ($read_nref,$read_fn,$read_sf);
		if ($read_b =~ /(\w+\.)(.+)/) {
			$read_nref = "$outdir/tmpreads/${1}not_repl.${2}";
			$read_fn = $1;
			chop $read_fn;
			$read_sf = $2;
		} else {
			die "Unexpected format of inputfile '$read_b', expected <name.suffix(es)>\n";
		}
		my $file_from = gz_path_check("$outdir/tmpreads/${1}not_repl.${2}",$modegz);
		my $file_to = gz_path_check("$outdir/benchmark_${read_fn}_se.${read_sf}",$modegz);
		copy("$file_from","$file_to") or die "Copy '$file_from' to '$file_to' failed: $!";
	}
}

#remove temporary ART and sequence files unless keep flag is set
if (! $keep) {
	rmrf("$outdir/tmpreads")
}

p_lock(0,1,"$outdir/tamock.log", "\n#################################################\nSummary\n");
my $STATS = r_file("$outdir/stats.log");
while (my $line = <$STATS>) {
	chomp $line;
	p_lock(0,1,"$outdir/tamock.log", "$line");
}

#remove temporary stats file from benchmark_profile_kreport.pl
rmrf("$outdir/stats.log");

$msg = "\nFinished tamock with runtime " . runtime(tv_interval($t0)) ;
p_lock(0,1,"$outdir/tamock.log", "$msg");

##########################################################################
sub install_dependencies
{
	my $unzip_path = can_run('unzip')
		or die "'unzip' not found, installation failed\n";
	my $zipinfo_path = can_run('zipinfo')
		or die "'zipinfo' not found, installation failed\n";
	my $make_path = can_run('make')
		or die "'make' not found, installation failed\n";
	
	#check if libGSL is installed
	can_run('gsl-config') or die "libgsl not installed (can't locate gsl-config)\n",
	"Please see 'https://www.gnu.org/software/gsl/' or use your local package manager for installation\n";
	
	#CENTRIFUGE
	print "Downloading Centrifuge...\n";
	
	#caveat, fetch fails with no error if https is used, just retype to http
	my $cf_arch = fetchfile('http://github.com/DaehwanKimLab/centrifuge/archive/6cc874e890249f6d8b479bd41b41e131f12c6796.zip',"$wd");
	runcmd("$unzip_path $cf_arch",0);
	
	print "Building Centrifuge...\n";
	#TODO find better generic solution to get directory name in archive
	my $cf_build = qx{$zipinfo_path -1 $cf_arch | head -n 1};
	chomp $cf_build;
	
	chdir $cf_build or die "Can't change to directory to '$cf_build': $!\n";
	runcmd("make",1);
	runcmd("make install prefix=${wd}/centrifuge",1);
	chdir $wd or die "Can't change to directory to '$wd': $!\n";
	
	rmrf($cf_arch);
	rmrf($cf_build);
	
	#ART
	print "Downloading ART...\n";
	#caveat, fetch fails with no error if https is used, just retype to http
	my $art_arch;
	if ("$Config{osname}" eq "linux") {
		$art_arch = fetchfile('https://www.niehs.nih.gov/research/resources/assets/docs/artsrcmountrainier2016.06.05linux.tgz',"$wd")
		
	} elsif ("$Config{osname}" eq "darwin") {
		$art_arch = fetchfile('https://www.niehs.nih.gov/research/resources/assets/docs/artsrcmountrainier2016.06.05macos.tgz',"$wd")
		
	} else {
		warn "Unknown OS '$Config{osname}', test only implemented for 'linux' and 'darwin' (MacOS)\nInstallation will assume Linux\n";
		$art_arch = fetchfile('https://www.niehs.nih.gov/research/resources/assets/docs/artsrcmountrainier2016.06.05linux.tgz',"$wd");
		
	}
	
	
	#my $art_arch = fetchfile('http://www.niehs.nih.gov/research/resources/assets/docs/artsrcmountrainier20160605linuxtgz.tgz',"$wd");
	
	my $art_tar = Archive::Tar->new;
	$art_tar->read("$art_arch");
	my $art_build = "${wd}/" . ($art_tar->list_files(['name']))[0];
	$art_tar->extract();
	
	print "Building ART...\n";
	chdir $art_build or die "Can't change to directory to '$art_build': $!\n";
	runcmd("./configure --prefix=${wd}/art",1);
	runcmd("make",1);
	runcmd("make install",1);
	chdir $wd or die "Can't change to directory to '$wd': $!\n";
	
	rmrf("$art_arch");
	rmrf("$art_build");
	
	#modify ART perl shebangs from /usr/bin/perl to /usr/bin/env perl
	my @ART_scripts = ("aln2bed.pl","art_profiler_454","combinedAvg.pl","empDist.pl","fastqReadAvg.pl","map2bed.pl","summation.pl");
	
	foreach my $script (@ART_scripts) {
		my $content;
		open my $IN, '<:encoding(UTF-8)',"$wd/art/bin/$script" or die "Couldn't read file '$wd/art/bin/$script' : $!";
		{
			local $/ = undef;
			$content = <$IN>;
		}
		close $IN;
		$content =~ s+/usr/bin/perl+/usr/bin/env perl+;
		open my $OUT, '>:encoding(UTF-8)',"$wd/art/bin/$script" or die "Couldn't write to file '$wd/art/bin/$script' : $!";
		print $OUT $content;
		close $OUT;
	}
	
	
	#get assembly summary file
	my $as_file = fetchfile("$ncbi_as_uri","$wd");
	
	print "Finished installing dependencies\n";
	
	exit;
}
##########################################################################
sub test_installation
{
	print "#################################################\n","Testing Installation...\n";
	
	#remove test_directory from previous run if present
	if (-d "${wd}/testdata/testout") {
		rmrf("${wd}/testdata/testout");
	}
	
	
	my $tamock_cmd = "${wd}/tamock -1 ${wd}/testdata/test_1.fq -2 ${wd}/testdata/test_2.fq";
	$tamock_cmd .= " -x ${wd}/testdata/sa_ec -R ${wd}/testdata/refgenomes -o ${wd}/testdata/testout --install-test-debug";
	
	my( $success, $error_message, $full_buf, $stdout_buf, $stderr_buf ) =
            run( command => "$tamock_cmd", verbose => 1 );
    
    if (! $success ) {
		die "'$tamock_cmd' failed: $error_message\n";
	}
	
	my $RS1 = r_file("${wd}/testdata/testout/benchmark_test_1.fq");
	my $RS2 = r_file("${wd}/testdata/testout/benchmark_test_2.fq");
	
	my $rsd1 = Digest::MD5->new();
	$rsd1->addfile($RS1);
	my $rs1_md5 = $rsd1->hexdigest();
	close $RS1;
	
	my $rsd2 = Digest::MD5->new();
	$rsd2->addfile($RS2);
	my $rs2_md5 = $rsd2->hexdigest();
	close $RS2;
	
	if ("$Config{osname}" eq "linux") {
		if ("$rs1_md5" eq "5d5b4ed75498cb661ebac10d6ee1ff17" && "$rs2_md5" eq "8711541cd59152efdabb7c6b2bc6d32b") {
			print "#################################################\n","Test successfull, everything is installed correctly\n";
		} else {
			print "#################################################\n","Test failed!\n",
			"Installation not successful, please report ${wd}/testdata/testout/warnings.log at https://github.com/gerners/tamock/issues\n";
		}
	} elsif ("$Config{osname}" eq "darwin") {
		if ("$rs1_md5" eq "b5e7dbedcd0e5414ac02168b766c82a5" && "$rs2_md5" eq "e5d5171bc6e9ab01f2918a514007254a") {
			print "#################################################\n","Test successfull, everything is installed correctly\n";
		} else {
			print "#################################################\n","Test failed!\n",
			"Installation not successful, please report ${wd}/testdata/testout/warnings.log at https://github.com/gerners/tamock/issues\n";
		}
	} else {
		warn "Unknown OS '$Config{osname}', test only implemented for 'linux' and 'darwin' (MacOS)\nInstallation success unknown\n";
	}
	
	
	
	exit;
}
##########################################################################
sub fetchfile
{
	my ($uri,$targetdir) = @_;
	if (! $targetdir) {
		$targetdir = ".";
	}
	my $ff = File::Fetch->new(uri => "$uri");
	my $file = $ff->fetch( to => "$targetdir") or die $ff->error();
	return $file;
}
##########################################################################
sub runcmd
{
	my ($cmd,$verbose) = @_;
	
	if ($install_deps) {
		$verbose = 1;
	} else {
		p_lock(0,0,"$outdir/tamock.log","Running [${cmd}]...\n");
		p_lock(1,0,"$outdir/warnings.log","Running [${cmd}]...\n");
	}	
	
	my( $success, $error_message, $full_buf, $stdout_buf, $stderr_buf ) =
            run( command => "$cmd", verbose => $verbose );
    #print newline after run() for better readability of progress in case of verbose, else p_lock adds the newline
    if ($verbose) {
    	print "\n";
    }
    
    if (! $install_deps) {
    	p_lock(0,0,"$outdir/tamock.log","@$stdout_buf");
		p_lock(1,0,"$outdir/warnings.log","@$stderr_buf");
    }
    
	if (! $success ) {
		die "'$cmd' failed: $error_message\n";
	}
}
##########################################################################
sub runtime
{
	#get runtime in seconds from e.g. "(time - $^T)"
	my $time = shift @_;
	my $rtime;
	#check if script ran for more than one minute
	if ($time > 59 ) {
		#or more than one hour
		if ($time > 3599 ) {
			#or more than one day
			if ($time > 86399) {
				$rtime = int($time / 86400) . "d " . int(($time % 86400) / 3600) . "h " . int((($time % 86400) % 3600) / 60) . "m " . (((time % 86400) % 3600) % 60) . "s";
				return $rtime;
			}
			$rtime = int($time / 3600) . "h " . int(($time % 3600) / 60) . "m " . (($time % 3600) % 60) . "s";
			return $rtime;	
		}
		$rtime = int($time / 60) . "m " . ($time % 60) . "s";
		return $rtime;
	}
	$rtime = $time . "s";
	return $rtime;
}
##########################################################################
sub rmrf
{
	my ($goner,$verbose) = @_;
	if (-d $goner) {
		my ($err);
		remove_tree("$goner", {verbose => $verbose, error => \$err});
		if (@$err) {
			for my $diag (@$err) {
				my ($file, $message) = %$diag;
				if ($file eq '') {
					print "Error: $message\n";
				} else {
					print "Problem unlinking $file: $message\n";
				}
			}
		}
	} else {
		unlink $goner or warn "Could not unlink $goner: $!";
	}
}
##########################################################################
sub mkd
{
	my ($dir,$verbose) = @_,
	my $err;
	make_path("$dir",{verbose => $verbose, error => \$err});
	if (@$err) {
		for my $diag (@$err) {
			my ($file, $message) = %$diag;
			if ($file eq '') {
				print "General error: $message\n";
			} else {
				print "Problem creating $file: $message\n";
			}
		}
	}
}
##########################################################################
sub r_file
{
	my $filepath = shift;
	my $FH;
	if ($filepath =~ /\.gz$/i) {
	 	$FH = IO::Uncompress::Gunzip->new("$filepath", MultiStream => 1 ) or die "ERROR: couldn't open file '$filepath' : $GunzipError\n";
	} else {
		open $FH, "$filepath" or die "ERROR: couldn't open file '$filepath' : $!";
	}
	return $FH;
}
##########################################################################
sub w_file
{
	my $filepath = shift;
	my $FH;
	
	if ($filepath =~ /\.gz$/i) {
	 	$FH = IO::Compress::Gzip->new("$filepath") or die "ERROR: couldn't write to file '$filepath' : $GzipError\n";
	} else {
		open $FH, ">$filepath" or die "ERROR: couldn't write to file '$filepath' : $!";
	}
	return $FH;
}
##########################################################################
sub a_file
{
	my $filepath = shift;
	my $FH;
	
	if ($filepath =~ /\.gz$/i) {
	 	$FH = IO::Compress::Gzip->new("$filepath", Append => 1) or die "ERROR: couldn't append to file '$filepath' : $GzipError\n";
	} else {
		open $FH, ">>$filepath" or die "ERROR: couldn't append to file '$filepath' : $!";
	}
	return $FH;
}
##########################################################################
sub p_lock
{
	my ($warn,$verbose,$log,$msg) = @_;
	my $FH = a_file("$log");
	flock $FH, LOCK_EX or die  "$0 [$$]: flock: $!";
	
	if (ref($msg) eq 'ARRAY') {
		$msg = join("",@$msg);
	}
	
	if ($verbose) {
		if ($warn) {
			warn "$msg\n";
		} else {
			print "$msg\n";
		}
		print $FH "$msg\n";
	} else {
		print $FH "$msg\n";
	}
	close $FH;
}
##########################################################################
sub gz_path_check
{
	my ($path,$gz) = @_;
	my $fpath;
	if ($gz && $path !~ /\.gz$/ ) {
		$fpath = "${path}.gz";
	} elsif ($gz && $path =~ /\.gz$/) {
		$fpath = $path;
	} elsif (! $gz && $path !~ /\.gz$/ ) {
		$fpath = $path;
	} elsif (! $gz && $path =~ /\.gz$/) {
		$fpath = substr($path,0,-3);
	} else {
		die "Unexpected filename of inputfile '$fpath', expected <name.suffix(es)>\n";
	}
	return $fpath;
}
##########################################################################
sub print_help
{
	print STDERR <<EOD;

Usage: $0 [Parameters]
	
	Version: $tamock_version
	
	tamock: Create benchmark data based on a metagenomic sample
	Default values in brackets
	
	==== Mandatory ====
	-1/--forward			Forward paired reads, optionally together with -U
	-2/--reverse			Reverse paired reads, optionally together with -U 
	-U/--unpaired			Unpaired/single end reads, mandatory if no paired reads
	-o/--outdir      		Output directory
	-R/--refgenomes			Directory to safe reference genomes in gzipped fasta format
						(if present, defaults to: $wd/refseq-genomes)
	-x/--index   			Centrifuge index (if present, defaults to: $wd/centrifuge-index/p+h+v)
	
	
	
	==== Optional ====
	
	-d/--domains			Select domains which should be simulated [EBVA]
					Options are E(ukaryota), B(acteria), V(iruses), A(rchaea)
					For multiple selections, provide comma separated values e.g. <-d E,B>. (B)
					
	--gzip    	        	Gzip all output sequence files (off)
	-k/--keep			Keep all intermediate ART and sequence files
						Temporary files are stored in <output_dir/tmpreads>	
	-a/--assembly-summary		NCBI RefSeq Assembly summary table to select reference genomes 
	                   			($wd/assembly_summary_refseq.txt)
	--ncbi-sum-update		Download current NCBI RefSeq Assembly summary table and exit
		                   		('$ncbi_as_uri')
	-t/--threads			Number of threads for centrifuge (1)
	-v/--verbose			Verbose mode, print detailed information to screen
	
	
	== Benchmarks ==
	
	--centrifuge-kreport		Pre-calculated Centrifuge kreport
	--centrifuge-out    		Pre-calculated Centrifuge tabbed output (-S <cent-out> centrifuge option)

	--no-reassign			No reassignment of read counts from strains without a reference to other 
	               				classified strains/reference genomes of same species (off)
	
	== ART sequence simulator ==
	
	-M/--illumina-model		Illumima error profile for ART. Either precalculated ART profile
	                        		or calculated from input sequences with option <custom> (custom)
	                        		For available pre-calculated profiles see 'art_illumina -h'
	-l/--length	    		Read length of simulated reads 
						(defaults to longest read length in first 250 reads of input file)
	--rn-sim  	        	Number of reads for simulated sequence fraction. By default, the same number of 	
	                        		sequences from the input sample will be kept, this option alters number of sequences!
	--mean-fragment-length		Mean size of fragments for paired-end simulations (200)
	--sd-fragment-length		Standard deviation of fragment size for paired-end simulations (10)
	--qprof1  	        	Precalculated forward-read quality profile for custom error profile in ART
	--qprof2  	        	Precalculated reverse-read quality profile for custom error profile in ART
	
	
	== Dependencies ==
	
	--install-deps			Install centrifuge and ART default versions (depends on libgsl)
	--install-test			Test installation
	
	
	
	-h/--help			Prints this helpmessage
	
EOD
	exit;
}